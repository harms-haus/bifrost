# Maintainer: Eric Siebeneich <devzeebo@users.noreply.github.com>
pkgname=bifrost-git
pkgver=r0
pkgrel=1
pkgdesc="Bifrost rune management server and CLI"
arch=('x86_64' 'aarch64')
url="https://github.com/devzeebo/bifrost"
license=('MIT')
makedepends=('go' 'git')
provides=('bifrost' 'bf')
conflicts=('bifrost')
backup=('etc/bifrost/bifrost.env')
source=("git+https://github.com/devzeebo/bifrost.git")
sha256sums=('SKIP')

pkgver() {
  cd bifrost
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd bifrost
  export CGO_ENABLED=0
  go build -ldflags='-s -w' -o bifrost-server ./server/cmd
  go build -ldflags='-s -w' -o bf ./cli/cmd/bf
}

package() {
  cd bifrost

  install -Dm755 bifrost-server "$pkgdir/usr/bin/bifrost-server"
  install -Dm755 bf "$pkgdir/usr/bin/bf"
  ln -s bf "$pkgdir/usr/bin/bifrost"

  install -Dm644 pkg/arch/bifrost.service "$pkgdir/usr/lib/systemd/system/bifrost.service"
  install -Dm640 pkg/arch/bifrost.env "$pkgdir/etc/bifrost/bifrost.env"
  install -Dm644 pkg/arch/bifrost.sysusers "$pkgdir/usr/lib/sysusers.d/bifrost.conf"
  install -Dm644 pkg/arch/bifrost.tmpfiles "$pkgdir/usr/lib/tmpfiles.d/bifrost.conf"

  install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
